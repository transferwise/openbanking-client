plugins {
    id 'java-library'
    id 'checkstyle'
    id 'pmd'
    id 'com.github.spotbugs' version '6.0.7'
    id 'org.hidetake.swagger.generator' version '2.19.2'
    id "com.diffplug.spotless" version "6.25.0"
}

ext.projectName = "Openbanking client"
ext.projectDescription = "Java client for using the UK Open Banking API."
ext.projectArtifactName = "openbanking-client"
ext.projectGitHubRepoName = "openbanking-client"
ext.authorName = "Michael Scott"
ext.authorGithub = "mscott-tw"
ext.authorEmail = "michael.scott@transferwise.com"

apply from: 'build.publishing.gradle'

group = 'com.transferwise'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

ext {
    libs = [
        'spring' : '6.0.19',
        'jackson': '2.15.4',
        'lombok' : '1.18.30',
        'junit'  : '5.9.3',
        'mockito': '5.3.1'
    ]
}

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    openApiGen
    swaggerCodegenV3
    // Getting Standard Commons Logging alert. Needs to ignore 'commons-logging'
    configureEach {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
}

dependencies {
    openApiGen("org.openapitools:openapi-generator-cli:3.3.4")
    swaggerCodegenV3("io.swagger.codegen.v3:swagger-codegen-cli:3.0.57")
    api("org.springframework:spring-web:${libs.spring}")

    implementation('org.apache.httpcomponents:httpclient:4.5.14')

    api("com.fasterxml.jackson.core:jackson-annotations:${libs.jackson}")
    api("com.fasterxml.jackson.core:jackson-databind:${libs.jackson}")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${libs.jackson}")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${libs.jackson}")

    api('org.bitbucket.b_c:jose4j:0.9.6')

    implementation('org.springframework.boot:spring-boot-starter-webflux:3.3.0')
    implementation('com.github.tomakehurst:wiremock-jre8-standalone:3.0.1')

    compileOnly("org.projectlombok:lombok:${libs.lombok}")
    testCompileOnly("org.projectlombok:lombok:${libs.lombok}")
    annotationProcessor("org.projectlombok:lombok:${libs.lombok}")
    testAnnotationProcessor("org.projectlombok:lombok:${libs.lombok}")

    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${libs.junit}")
    testImplementation("org.junit.jupiter:junit-jupiter:${libs.junit}")
    testImplementation("org.mockito:mockito-core:${libs.mockito}")
    testImplementation("org.mockito:mockito-junit-jupiter:${libs.mockito}")
    testImplementation("org.hamcrest:hamcrest:2.2")
    testImplementation("org.skyscreamer:jsonassert:1.5.1")
    testImplementation("org.springframework:spring-test:${libs.spring}")
    testImplementation("org.bouncycastle:bcpkix-jdk15on:1.70")
    testImplementation("ch.qos.logback:logback-classic:1.5.6")
}

swaggerSources {
    paymentInitiation {
        inputFile = file('specs/payment-initiation-v3-swagger.yaml')
        code {
            language = 'java'
            components = ['models']
            configFile = file('specs/payment-initiation-v3-generator-config.json')
            configuration = configurations.swaggerCodegenV3
        }
    }
    vrp {
        inputFile = file('specs/vrp-openapi.yaml')
        code {
            language = 'java'
            components = ['models']
            configFile = file('specs/vrp-openapi-config.json')
            configuration = configurations.openApiGen
        }
    }
    event {
        inputFile = file('specs/events-openapi.yaml')
        code {
            language = 'java'
            components = ['models']
            configFile = file('specs/events-openapi-config.json')
            configuration = configurations.openApiGen
        }
    }
    eventNotifications {
        inputFile = file('specs/event-notifications-openapi.yaml')
        code {
            language = 'java'
            components = ['models']
            configFile = file('specs/event-notifications-openapi.json')
            configuration = configurations.openApiGen
        }
    }
}

compileJava.dependsOn(generateSwaggerCode)

sourceSets.main.java.srcDir "${swaggerSources.paymentInitiation.code.outputDir}/src/main/java"
sourceSets.main.java.srcDir "${swaggerSources.vrp.code.outputDir}/src/main/java"
sourceSets.main.java.srcDir "${swaggerSources.event.code.outputDir}/src/main/java"
sourceSets.main.java.srcDir "${swaggerSources.eventNotifications.code.outputDir}/src/main/java"

spotless {
    java {
        target layout.files(
            "${swaggerSources.paymentInitiation.code.outputDir}/src/main/java",
            "${swaggerSources.vrp.code.outputDir}/src/main/java",
            "${swaggerSources.event.code.outputDir}/src/main/java",
            "${swaggerSources.eventNotifications.code.outputDir}/src/main/java")
        replaceRegex 'Remove swagger @ApiModel annotations', '@ApiModel.*$', ''
        replaceRegex 'Remove swagger @ApiModelProperty annotations', '@ApiModelProperty.*$', ''
        replaceRegex 'Remove swagger @Schema annotations', '@Schema.*$', ''
        removeUnusedImports()
    }
}

compileJava.dependsOn(spotlessApply)

jar {
    doFirst {
        manifest {
            attributes("Implementation-Title": project.name, "Implementation-Version": project.version)
        }
    }
}

checkstyle {
    configFile = new File(rootDir, "checkstyle.xml")
    sourceSets = [project.sourceSets.main, project.sourceSets.test]
    ignoreFailures = false
    maxWarnings = 0
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
    // exclude the automatically generated models, we have limited control over how they're generated
    exclude '**/com/transferwise/openbanking/client/api/payment/v3/model/**'
}

spotbugs {
    excludeFilter = file('spotbugs_exclude_filter.xml')
}

spotbugsMain {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

spotbugsTest {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

pmd {
    incrementalAnalysis = true
    ruleSetFiles = files("custom-pmd-rules.xml")
    ruleSets = []
}

tasks.withType(Pmd) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
    // exclude the automatically generated models, we have limited control over how they're generated
    exclude '**/com/transferwise/openbanking/client/api/payment/v3/model/**'
}

wrapper {
    gradleVersion = '7.6'
}

test {
    useJUnitPlatform()
}
